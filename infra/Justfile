db_name := "hello-blog-database"
frontend_image_name := "hello-blog-fe"
backend_image_name := "hello-blog-be"
integrated_image_name := "hello-blog-integrated"

default:
  just --list

@create-db-checkpoint connection_string="mongodb://localhost:27117/?replicaSet=rs0&directConnection=true":
  echo 'creating checkpoint for database {{db_name}} with connection string {{connection_string}}'
  mongodump --uri={{connection_string}} --db={{db_name}} --out=db_checkpoint --quiet

@restore-db-checkpoint connection_string="mongodb://localhost:27117/?replicaSet=rs0&directConnection=true" destination_container="hb-database":
  echo 'retoring checkpoint for database {{db_name}}  with connection string {{connection_string}} on container {{destination_container}}'
  docker exec hb-database mongosh --quiet --eval "rs.initiate();" && \
  mongorestore --drop --uri={{connection_string}} --db={{db_name}} db_checkpoint/{{db_name}}/ --quiet

@build-docker-image dockerfile image_name image_tag:
  docker build -t {{image_name}}:{{image_tag}} -f {{dockerfile}} ..

@build-backend-image:
  echo "Building docker container for backend service with file Dockerfile-backend with working directory $(cd .. && pwd)"
  just build-docker-image Dockerfile-backend {{backend_image_name}} latest

@build-frontend-image:
  echo "Building docker container for frontend service with file Dockerfile-frontend with working directory $(cd .. && pwd)"
  just build-docker-image Dockerfile-frontend {{frontend_image_name}} latest

@build-backend-integrated-image:
  echo "Building docker container for integrated backend service with file Dockerfile-integrated with working directory $(cd .. && pwd)"
  just build-docker-image Dockerfile-integrated {{integrated_image_name}} latest